apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply plugin: 'checkstyle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        versionName VERSION_NAME
        versionCode Integer.parseInt(VERSION_CODE)
        minSdkVersion rootProject.ext.minSdkVersion

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    packagingOptions {
        exclude 'LICENSE.txt' // multiple libs have this file -> cause build error
    }

    lintOptions {
        disable 'InvalidPackage' // AssertJ references java.nio package, which is not available on Android
    }
}

// We declare all libraries in the root build.gradle, see them there
dependencies {
    compile libraries.supportAnnotations

    testCompile libraries.junit
    testCompile libraries.assertJ
    testCompile libraries.mockitoCore
    testCompile libraries.powerMockJUnit
    testCompile libraries.powerMockMockito
}

task checkstyle(type: Checkstyle) {
    description 'Analyzing project and creating report with bad things in the code'

    ignoreFailures = false
    configFile file('../checkstyle.xml')

    source 'src'
    include '**/*.java'

    classpath = files()
}

apply from: '../gradle/jacoco-android.gradle'
apply from: '../gradle/publish-android-lib.gradle'